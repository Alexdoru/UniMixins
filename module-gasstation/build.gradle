apply from: "${rootDir}/module-conventions.gradle"

dependencies {
	compileOnly("org.spongepowered:mixin:$gasMixVersion")
	compileOnly project(':module-mixinbooterlegacy')
	shadow(project(":module-common")) {
		transitive = false
	}
}

archivesBaseName = archivesBaseName.replace("-1.7.10", "-gasstation-1.7.10")

shadowJar {
	configurations = [project.configurations.shadow]
	archiveClassifier = 'dev'
	relocate 'io.github.legacymoddingmc.unimixins.common', 'io.github.legacymoddingmc.unimixins.gasstation.repackage.common'

	manifest {
		attributes (
			'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
			'FMLCorePluginContainsFMLMod': true,
			'ForceLoadAsMod': true,
			'MixinConfigs': 'mixins.gasstation.json',
			'FMLCorePlugin': 'com.falsepattern.gasstation.core.GasStationCore'
		)
	}
}

jar {
	enabled = false
	dependsOn shadowJar
}

ext.uniVersion = version
ext.mcversion = "1.7.10"
version += "+" + ext.extraVersion

minecraft {
	injectedTags.put("MODID", "gasstation")
	injectedTags.put("MODNAME", "GasStation")
	injectedTags.put("VERSION", extraVersion)
	injectedTags.put("GROUPNAME", "com.falsepattern.gasstation")
}

tasks.injectTags.configure {
	outputClassName.set("com.falsepattern.gasstation.Tags")
}
