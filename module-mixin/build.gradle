import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

buildscript {
	repositories {
		gradlePluginPortal()
	}
	dependencies {
		classpath 'com.github.jengelman.gradle.plugins:shadow:6.1.0'
	}
}

repositories {
	maven {
		name = 'sponge'
		url = 'https://repo.spongepowered.org/maven/'
	}
	maven {
		url = "https://mvn.falsepattern.com/releases"
	}
	maven {
		name = "GTNH Maven"
		url = "http://jenkins.usrv.eu:8081/nexus/content/groups/public/"
		allowInsecureProtocol = true
	}
	mavenLocal()
}

def versionBase = version
archivesBaseName = archivesBaseName.replace("-1.7.10", "-mixin-1.7.10")

configurations.archives.artifacts.clear()

apply plugin: "com.github.johnrengelman.shadow"

["spongepowered", "fabric", "gasmix", "gtnh", "unimix"].each {
	def mixinFlavor = it
	def _flavor = it.capitalize()
	def mixinFlavorClassifier = "UNKNOWN"
	def mixinFlavorCapitalized = [spongepowered: "SpongePowered", fabric: "Fabric", gasmix: "GasMix", gtnh: "GTNH", unimix: "UniMix"][mixinFlavor]

	configurations.create("shadow$_flavor")
	configurations.create("shadowBridge$_flavor")

	if (mixinFlavor == "fabric" || mixinFlavor == "spongepowered" || mixinFlavor == "unimix" || mixinFlavor == "gtnh") {
		// Fabric / SpongePowered / UniMix / GTNH
		
		if (mixinFlavor == "fabric" || mixinFlavor == "unimix") {
			def mixinVersion = mixinFlavor == "fabric" ? "0.11.4+mixin.0.8.5" : "0.11.5+mixin.0.8.5"
			if (mixinFlavor == "unimix" && (project.hasProperty("local") || true)) {
				mixinVersion += "-local"
			}
			def mixinGroup = mixinFlavor == "fabric" ? "net.fabricmc" : "io.github.legacymoddingmc"
			def mixinDep = mixinFlavor == "fabric" ? "net.fabricmc:sponge-mixin:$mixinVersion" : "io.github.legacymoddingmc:sponge-mixin:$mixinVersion"
			mixinFlavorClassifier = "$mixinFlavor-$mixinVersion"
			
			dependencies {
				add("shadow$_flavor", "$mixinGroup:sponge-mixin:$mixinVersion", {
					exclude group: "org.ow2.asm"
				})
				add("shadow$_flavor", 'org.ow2.asm:asm-tree:9.4')
				add("shadow$_flavor", 'org.ow2.asm:asm-commons:9.4')
				add("shadow$_flavor", 'org.ow2.asm:asm-util:9.4')

				add("shadowBridge$_flavor", "$mixinGroup:sponge-mixin:$mixinVersion", {
					transitive = false
				})
			}
		} else if(mixinFlavor == "spongepowered") {
			mixinFlavorClassifier = "spongepowered-0.8.5"
			
			dependencies {
				add("shadow$_flavor", 'org.spongepowered:mixin:0.8.5')
				add("shadowBridge$_flavor", 'org.spongepowered:mixin:0.8.5', {
					transitive = false
				})
				add("shadow$_flavor", 'com.google.guava:guava:21.0')
				add("shadow$_flavor", 'com.google.code.gson:gson:2.2.4')
				add("shadow$_flavor", 'org.ow2.asm:asm-tree:9.2')
				add("shadow$_flavor", 'org.ow2.asm:asm-commons:9.2')
				add("shadow$_flavor", 'org.ow2.asm:asm-util:9.2')
			}
		} else if(mixinFlavor == "gtnh") {
			// Adapted from GTNHMixins's build script

			def mixinVersion = "0.8.5-GTNH-2"
			mixinFlavorClassifier = "gtnh-$mixinVersion"
			def asmVersion = "9.4"
			
			dependencies {
				add("shadow$_flavor", "org.spongepowered:mixin:$mixinVersion")
				add("shadow$_flavor", "org.ow2.asm:asm-tree:$asmVersion")
				add("shadow$_flavor", "org.ow2.asm:asm-commons:$asmVersion")
				add("shadow$_flavor", "org.ow2.asm:asm-util:$asmVersion")
				add("shadow$_flavor", "com.google.guava:guava:21.0")
				add("shadowBridge$_flavor", 'org.spongepowered:mixin:0.8.5')
			}

		}

		// We want to *not* relocate ASM in the bridge classes. So we use a multi-step
		// build procedure:

		// 1. Create relocated Mixin jar, without the bridge classes
		task("mixinJar$_flavor", type: ShadowJar) {
			destinationDirectory = file("build/tmp")
			classifier = "tmpMixin$_flavor"
			configurations = [project.configurations."shadow$_flavor"]

			relocate 'org.objectweb.asm', 'org.spongepowered.asm.lib'
			if(mixinFlavor != "gtnh") {
				relocate 'com.google', 'org.spongepowered.libraries.com.google'
			} else {
				// we don't use this ASM package name
				//relocate 'org.objectweb.asm', 'org.spongepowered.libraries.org.objectweb.asm'
				relocate 'com.google.common', 'org.spongepowered.libraries.com.google.common'
				relocate 'com.google.thirdparty.publicsuffix', 'org.spongepowered.libraries.com.google.thirdparty.publicsuffix'
			}

			exclude 'org/spongepowered/asm/bridge/RemapperAdapter.class'
			exclude 'org/spongepowered/asm/bridge/RemapperAdapterFML.class'

			// Exclude stuff that's compiled for Java 16

			exclude 'org/spongepowered/asm/service/modlauncher/*'
			exclude 'org/spongepowered/asm/launch/MixinTransformationServiceLegacy*'
			exclude 'org/spongepowered/asm/launch/MixinLaunchPlugin*'
			exclude 'org/spongepowered/asm/launch/MixinTransformationService*'
			exclude 'org/spongepowered/asm/launch/platform/container/ContainerHandleModLauncherEx*'

			exclude 'META-INF/services/cpw.mods.modlauncher.api.ITransformationService'
			exclude 'META-INF/services/cpw.mods.modlauncher.serviceapi.ILaunchPluginService'
			
			exclude '**/module-info.class'

			// Exclude jar-specific stuff

			exclude 'META-INF/MANIFEST.MF', 'META-INF/maven/**', 'META-INF/*.RSA', 'META-INF/*.SF'
		}

		// 2. Create Mixin jar without relocation, with *only* the bridge classes
		task("bridgeJar$_flavor", type: ShadowJar) {
			destinationDirectory = file("build/tmp")
			classifier = "tmpBridge$_flavor"
			configurations = [project.configurations."shadowBridge$_flavor"]

			include 'org/spongepowered/asm/bridge/RemapperAdapter.class'
			include 'org/spongepowered/asm/bridge/RemapperAdapterFML.class'
		}

		// 3. Combine the two jars
		task("shadowJar$_flavor", type: ShadowJar) {
			version = versionBase + "+" + mixinFlavorClassifier
			from(sourceSets.main.output) {
				exclude "mcmod.info"
			}

			dependsOn "mixinJar$_flavor"
			dependsOn "bridgeJar$_flavor"
			dependsOn "mixinJar$_flavor"
			dependsOn "bridgeJar$_flavor"

			from zipTree(tasks."mixinJar$_flavor".archiveFile).matching { exclude 'module-info.class' }
			from zipTree(tasks."bridgeJar$_flavor".archiveFile).matching { include 'org/spongepowered/asm/bridge/*' }

			doLast {
				delete tasks."mixinJar$_flavor".archiveFile
				delete tasks."bridgeJar$_flavor".archiveFile
			}

			manifest {
				attributes(
					'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
					'FMLCorePluginContainsFMLMod': 'true',
					'ForceLoadAsMod': 'true',
				)
			}
		}

		jar.dependsOn("shadowJar$_flavor")
	} else if (mixinFlavor == "gasmix") {
		// GasMix
		
		mixinFlavorClassifier = "gasmix-0.8.5-gasstation_7"
		
		dependencies {
			add("shadow$_flavor", 'org.spongepowered:mixin:0.8.5-gasstation_7')
		}

		task("shadowJar$_flavor", type: ShadowJar) {
			from(sourceSets.main.output) {
				exclude "mcmod.info"
			}

			version = versionBase + "+" + mixinFlavorClassifier
			classifier = ''
			configurations = [project.configurations."shadow$_flavor"]

			manifest {
				attributes(
					'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
					'FMLCorePluginContainsFMLMod': 'true',
					'ForceLoadAsMod': 'true',
				)
			}
		}

		jar.dependsOn("shadowJar$_flavor")
	}

	// Common

	task("createMcmodInfo$_flavor", type: Copy) {
		outputs.upToDateWhen { false }
		from 'src/main/resources/mcmod.info'
		into "build/tmp/mcmod.${mixinFlavor}.info"
		filter {
			line -> line.replaceAll('@MIXIN_CLASSIFIER@', mixinFlavorClassifier).replaceAll('@MIXIN_SOURCE_CAPITALIZED@', mixinFlavorCapitalized).replaceAll('@VERSION@', "$versionBase+$mixinFlavorClassifier")
		}
	}

	tasks."shadowJar$_flavor" {
		dependsOn("createMcmodInfo$_flavor")
		from "build/tmp/mcmod.${mixinFlavor}.info/mcmod.info"
	}

	publishing {
		publications {
			create("maven$_flavor", MavenPublication) {
				artifact tasks."shadowJar$_flavor"

				artifactId = archivesBaseName
				version = tasks."shadowJar$_flavor".version
			}
		}
	}
}

// Obfuscation is useless because we're not compiling code referencing Minecraft classes
reobf.enabled = false

jar.enabled = false
sourcesJar.enabled = false
